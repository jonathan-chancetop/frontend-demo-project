apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.framework.demo'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
        }
    }
}

def coreNGVersion = '5.2.8.1'

project(':website') {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        compile "core.framework:core-ng:${coreNGVersion}"
        testCompile "core.framework:core-ng-test:${coreNGVersion}"
    }
}

def yarn(String dir, String... params) {
    def isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    def command = isWindows ? ['cmd', '/c', 'yarn'] : ['yarn']
    command.addAll(params)
    return {
        workingDir dir
        commandLine command
    }
}

task buildFrontend {
    def frontendDir = "${rootDir}/website-frontend"
    doLast {
        exec yarn(frontendDir, 'install')
        exec yarn(frontendDir, 'run', 'build')
        delete "${rootDir}/website/src/main/dist/web"
        copy {
            from "${frontendDir}/build/dist"
            into "${rootDir}/website/src/main/dist/web"
        }
    }
}
