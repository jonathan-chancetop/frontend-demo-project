defaultTasks 'clean', 'build'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.3'
    }
}

def buildTime = new Date()

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: org.junit.platform.gradle.plugin.JUnitPlatformPlugin

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-Xlint:all'
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    buildDir = "${rootDir}/build/${name}"

    dependencies {
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.3'
    }

    afterEvaluate {
        tasks.withType(Jar) {
            manifest {
                attributes 'Implementation-Title': project.name,
                        'Implementation-Version': project.version,
                        'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                        'Built-With': "gradle-${project.gradle.gradleVersion}, groovy-${GroovySystem.version}",
                        'Built-By': System.getProperty('user.name'),
                        'Built-On': "${InetAddress.localHost.hostName}/${InetAddress.localHost.hostAddress}",
                        'Build-Time': buildTime.format('yyyy/MM/dd HH:mm:ss')
            }
        }
    }
}

apply from: file("${rootDir}/gradle/check.gradle")

allprojects {
    apply plugin: 'idea'
    idea.module.inheritOutputDirs = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}
